finish
/clear

c***
c*** parametri
c***
l=100
b=25
d=25
ne1=30
ne2=25
ne3=80
p=10

/prep7
mp,ex,1,205000
mp,prxy,1,0.3
et,1,182
keyopt,1,3,0   ! 0 - plane stress (def), 2 - plane strain

! bottom-up

!keypoints
k,1,d/2,0
k,2,b,0
k,3,b,b
k,4,b,l
k,5,0,l
k,6,0,b
k,7,0,d/2
k,8,0,0

! linee

l,1,2
l,2,3
l,3,4
l,3,6
l,4,5
l,5,6
l,6,7
larc,7,1,8,d/2

kdele,8

! aree

al,1,2,4,7,8
al,4,3,5,6

! mesh

lsel,,loc,y,0
lesize,all,,,ne1
lsel,,loc,y,d/2,b
lsel,r,loc,x,0
lesize,all,,,ne1
alls
csys,1
lsel,,loc,x,d/2
lesize,all,,,ne2*2
lsel,,loc,x,b,b*2
lesize,all,,,ne2
lccat,all
alls
csys,0
lsel,,loc,y,b+0.1,l-0.1
lesize,all,,,ne3
lsel,,loc,y,l
lesize,all,,,ne2
alls
amesh,all

! refine nella zona di concentrazione delle tensioni

nsel,s,loc,x,d/2-d/4,d/2+d/4
nsel,r,loc,y,-0.1,+d/4
nrefine,all
alls

! vincoli e carico

nsel,s,loc,y,0
d,all,uy,0
nsel,s,loc,x,0
d,all,ux,0
alls
nsel,s,loc,y,l
sf,all,pres,-p
alls

! soluzione

/solu
solve

! postprocessing

/post1
plnsol,s,y