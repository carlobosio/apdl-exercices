finish 
/clear 

c***
c*** parametri
c***

! geometria 

l = 2400
b = 100
h = 100
e = 10
h1 = h-e
a = 6

! carico
p = 10000/(2*l*b) ! [MPa]

/prep7

! materiale

mp,ex,1,205000
mp,prxy,1,0.3
et,1,181
keyopt,1,5,1 ! formulazione avanzata shell con curvatura

sectype,1,shell ! ali
secdata,e
sectype,2,shell ! anima (met√†)
secdata,a/2

c*** geometria

k,1,0,0,0
k,2,0,h1/2,0
k,3,b/2,h1/2,0

k,4,0,0,l/2
k,5,0,h1/2,l/2
k,6,b/2,h1/2,l/2

a,2,3,6,5
a,1,2,5,4

! mesh

esize,10
secnum,1
amesh,1

secnum,2
amesh,2

! vincoli

nsel,s,loc,z,0
d,all,uy,0

! simmetrie

nsel,s,loc,x,-0.1,0.1
d,all,ux,0
d,all,roty,0
d,all,rotz,all

nsel,s,loc,z,l/2-0.1,l/2+0.1
d,all,uz,0
d,all,rotx,0
d,all,roty,0

nsel,s,loc,y,-0.1,0.1
d,all,ux,0
d,all,uz,0
d,all,roty,0

! carichi

sfa,1,,press,p
alls

finish

! soluzione
/solu
solve 
finish

! post-processing
/post1
/view,1,1,1,1
plnsol,u,y
!/eof
plnsol,s,z



